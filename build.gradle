buildscript {
    ext {
        springBootVersion = '2.7.5'
        springCloudVersion = '2021.0.3'
        queryDslVersion = '5.0.0'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'net.huray'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'io.springfox:springfox-boot-starter:3.0.0'
        implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
        annotationProcessor(
                "javax.persistence:javax.persistence-api",
                "javax.annotation:javax.annotation-api",
                "com.querydsl:querydsl-apt:${queryDslVersion}:jpa"
        )
        implementation 'org.modelmapper:modelmapper:3.1.0'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    sourceSets {
        main.java.srcDir "$buildDir/generated/querydsl"
    }

}